from Standard.Base import all
import Standard.Base.Runtime.Ref.Ref

from Standard.Test import Bench, Faker
from Standard.Base.Runtime import assert


options = Bench.options . set_warmup (Bench.phase_conf 1 3) . set_measure (Bench.phase_conf 1 3)

type Data
    Value ~floats ~integers

    create vector_size faker =
        Data.Value (create_floats vector_size faker) (create_integers vector_size faker)


create_floats vector_size faker =
    Vector.new vector_size _-> faker.float -1000000000 1000000000


create_integers vector_size faker =
    Vector.new vector_size _-> faker.integer -1000000000 1000000000


collect_benches = Bench.build builder->
    vector_size = 1000000
    ## No specific significance to this constant, just fixed to make generated set deterministic
    fixed_random_seed = 1644575867
    faker = Faker.new fixed_random_seed
    funs = [["ceil", _.ceil], ["floor", _.floor], ["truncate", _.truncate]]

    data = Data.create vector_size faker

    builder.group "Numeric_Rounding" options group_builder->
        funs.each pair->
            name = pair.at 0
            fun = pair.at 1
            group_builder.specify ("float_" + name) (data.floats.map fun)

        funs.each pair->
            name = pair.at 0
            fun = pair.at 1
            group_builder.specify ("integer_" + name) (data.integers.map fun)

        [True, False].each use_bankers->
            [0, -2, 2].each decimal_places->
                name = create_name "round_decimal_places_" decimal_places use_bankers
                fun = _.round decimal_places use_bankers
                group_builder.specify ("float_" + name) (data.floats.map fun)
                group_builder.specify ("integer_" + name) (data.integers.map fun)

    up_to_options = Bench.options . set_warmup (Bench.phase_conf 5 2) . set_measure (Bench.phase_conf 3 2)

    builder.group "Numeric_Up_To" up_to_options group_builder->
        group_builder.specify "counting_up_to_1000000" <|
            cell = Ref.new 0
            n = 100000000
            0.up_to n . each _->
                x = cell.get
                cell.put x+1
            assert cell.get==n "Expecting "+n.to_text+" but was "+cell.get.to_text

## Creates a valid name for the benchmark
create_name : Text -> Integer -> Boolean -> Text
create_name prefix dec_places use_bankers =
    use_bankers_str = "_use_bankers_" + (use_bankers.to_text.to_case Case.Lower)
    case dec_places >= 0 of
        True -> prefix + dec_places.to_text + use_bankers_str
        False -> prefix + "minus_" + (dec_places.abs.to_text) + use_bankers_str


main = collect_benches . run_main
